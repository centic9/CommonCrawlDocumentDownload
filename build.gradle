plugins {
	// https://github.com/researchgate/gradle-release
	id 'net.researchgate.release' version '2.8.1'
    id 'io.codearte.nexus-staging' version '0.21.2'
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'

sourceCompatibility = 1.8
group = 'org.dstadler'
archivesBaseName = 'commoncrawldownload'

repositories {
	mavenCentral()
}

configurations.all {
	all*.exclude group: 'org.apache.hadoop'		// not found on Maven Central?
}

dependencies {
    compile 'org.dstadler:commons-dost:1.0.0.31'
    compile 'org.apache.httpcomponents:httpclient:4.5.12'
    compile 'org.netpreserve.commons:webarchive-commons:1.1.8'
    compile 'com.fasterxml.jackson.core:jackson-core:2.11.2'

    // for commoncrawl-source
    compile 'log4j:log4j:1.2.17'
    compile 'org.jsoup:jsoup:1.11.3'

    // for @Nullable
    compile 'com.google.code.findbugs:jsr305:3.0.2'

    testCompile 'junit:junit:4.13'
	testCompile 'org.dstadler:commons-test:1.0.0.18'
}

wrapper {
    gradleVersion = '5.6.4'
}

test {
	systemProperties = System.properties

	// enable to show standard out and standard error of the test JVM(s) on the console
	// testLogging.showStandardStreams = true

	// http://forums.gradle.org/gradle/topics/jacoco_related_failure_in_multiproject_build
	systemProperties['user.dir'] = workingDir
}

jacoco {
	toolVersion = '0.8.5'
}

jacocoTestReport {
    reports {
        xml.enabled true
	}
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

//provide defaults so we do not need to specify them always
if (!project.hasProperty('ossrhUsername')) {
    ext.ossrhUsername = ''
}
if (!project.hasProperty('ossrhPassword')) {
    ext.ossrhPassword = ''
}

uploadArchives {
  repositories {
    mavenDeployer {
        //noinspection GrUnresolvedAccess
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

    pom.project {
        name 'CommonCrawl Document Download'
        packaging 'jar'
        // optionally artifactId can be defined here
        description 'Common utilities I find useful in many of my projects.'
				url 'https://github.com/centic9/CommonCrawlDocumentDownload'

        scm {
            connection 'scm:git@github.com:centic9/CommonCrawlDocumentDownload'
            developerConnection 'scm:git@github.com:centic9/CommonCrawlDocumentDownload'
            url 'https://github.com/centic9/CommonCrawlDocumentDownload'
        }

        licenses {
          license {
            name 'BSD 2-Clause License'
            url 'http://www.opensource.org/licenses/bsd-license.php'
          }
        }

        developers {
          developer {
            id 'centic9	'
            name 'Dominik Stadler'
          }
        }
      }
    }
  }
}

afterReleaseBuild.dependsOn uploadArchives

task lookupURLs(type:JavaExec,dependsOn: compileJava) {
	description = 'Reads the current Common Crawl URL index data and extracts all URLs for interesting mime-types or file extensions'

    jvmArgs = [
            '-Xmx128m'
    ]

    main = 'org.dstadler.commoncrawl.index.DownloadURLIndex'
	classpath = sourceSets.main.runtimeClasspath
}


task downloadOldIndex(type:JavaExec,dependsOn: compileJava) {
	description = 'Reading blocks at beginning or the last stored block-number and download binary data from the common crawl archives'

    jvmArgs = [
            '-Xmx128m'
    ]

    main = 'org.dstadler.commoncrawl.oldindex.ReadAndDownload'
	classpath = sourceSets.main.runtimeClasspath
}

task downloadDocuments(type:JavaExec,dependsOn: compileJava) {
	description = 'Uses the URLs listed in commoncrawl.txt to download the documents from the Common Crawl'

    jvmArgs = [
            '-Xmx128m'
    ]

    main = 'org.dstadler.commoncrawl.index.DownloadFromCommonCrawl'
	classpath = sourceSets.main.runtimeClasspath
}
